buildscript {
    ext {
        grgitVersion = '2.2.1'
        aptPluginVersion = '0.15'
    }
    repositories {
        maven { url 'https://plugins.gradle.org/m2/' }
        mavenCentral()
    }
    dependencies {
        classpath "org.ajoberstar:grgit:${grgitVersion}"
        classpath "net.ltgt.gradle:gradle-apt-plugin:${aptPluginVersion}"
    }
}

apply plugin: 'idea'
apply plugin: 'java'
apply plugin: 'java-library'
apply plugin: 'maven-publish'
apply plugin: 'org.ajoberstar.grgit'
apply plugin: 'net.ltgt.apt-idea'


group = "space.npstr.magma"
version = "${versionFromTag()}"

sourceCompatibility = 10
targetCompatibility = 10

repositories {
    jcenter()
    mavenCentral()
    maven { url 'https://jitpack.io' }
}

ext {
    //@formatter:off
    gradleVersion              = '4.7'

    opusJavaVersion             = '1.0.0'
    orgJsonVersion              = '20180130'
    slf4jVersion                = '1.7.25'

    springVersion               = '5.0.6.RELEASE'
    undertowVersion             = '2.0.7.Final'

    spotbugsVersion             = '3.1.3'
    annotationsVersion          = '0.0.1'

    immutablesVersion           = '2.6.0'

    junitVersion                = '5.2.0'
    logbackVersion              = '1.2.3'

    //@formatter:on
}

dependencies {
    //@formatter:off
    compile             group: 'club.minnced',          name: 'opus-java',              version: opusJavaVersion
    compile             group: 'org.json',              name: 'json',                   version: orgJsonVersion
    compile             group: 'org.slf4j',             name: 'slf4j-api',              version: slf4jVersion

    // project reactor & reactive websocket client
    compile             group: 'org.springframework',   name: 'spring-webflux',         version: springVersion
    compile             group: 'io.undertow',           name: 'undertow-core',          version: undertowVersion

    // annotations
    compileOnly         group: 'com.github.spotbugs',   name: 'spotbugs-annotations',   version: spotbugsVersion
    compileOnly         group: 'space.npstr',           name: 'annotations',            version: annotationsVersion

    // immutable objects, to pass values around
    annotationProcessor group: 'org.immutables',        name: 'value',                  version: immutablesVersion
    compileOnly         group: 'org.immutables',        name: 'value',                  version: immutablesVersion, classifier: 'annotations'

    // testing
    testCompile         group: 'org.junit.jupiter',     name: 'junit-jupiter-api',      version: junitVersion
    testRuntime         group: 'ch.qos.logback',        name: 'logback-classic',        version: logbackVersion
    testRuntime         group: 'org.junit.jupiter',     name: 'junit-jupiter-engine',   version: junitVersion
    //@formatter:on
}

compileJava {
    dependsOn(clean)
    options.encoding = 'UTF-8'
    compileJava.options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
}

task wrapper(type: Wrapper) {
    gradleVersion = project.ext.gradleVersion
    //noinspection UnnecessaryQualifiedReference
    distributionType = Wrapper.DistributionType.ALL
}


build {
    doLast {
        println 'Version: ' + version
    }
}

test {
    useJUnitPlatform()
}

jar.mustRunAfter clean
publishToMavenLocal.dependsOn jar
// called by jitpack
task install {
    dependsOn test
    dependsOn publishToMavenLocal
    doLast {
        println 'Version: ' + version
    }
}
task sourceJar(type: Jar) {
    from sourceSets.main.allJava
}

def pomConfig = {
    licenses {
        license {
            //noinspection GroovyAssignabilityCheck
            name 'Apache License 2.0'
            url 'https://choosealicense.com/licenses/apache-2.0/'
            distribution 'repo'
        }
    }
    developers {
        developer {
            //noinspection GroovyAssignabilityCheck
            name 'Napster'
            email 'napster@npstr.space'
        }
    }
    scm {
        url 'https://github.com/napstr/Magma'
    }
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            artifact sourceJar {
                classifier 'sources'
            }
            groupId project.group
            artifactId project.name
            version project.version
            pom.withXml {
                def root = asNode()
                root.appendNode('description', 'A voice only API for Discord, focused on delivering music at scale.')
                root.appendNode('name', 'Magma')
                root.appendNode('url', 'https://github.com/napstr/Magma')
                root.children().last() + pomConfig
            }
        }
    }
}


idea {
    project {
        configureAnnotationProcessing = true
    }
    module {
        apt {
            addGeneratedSourcesDirs = true
            addAptDependencies = true
            addCompileOnlyDependencies = false
            mainDependenciesScope = 'PROVIDED'
        }
    }
}

//returns either a git tag if there is one on this commit, or the commit hash, to be used as a version
@SuppressWarnings("GrMethodMayBeStatic")
String versionFromTag() {

    def headTag = grgit.tag.list().find {
        it.commit == grgit.head()
    }

    def clean = grgit.status().clean //uncommited changes? -> should be SNAPSHOT

    if (headTag && clean) {
        headTag.getName()
    } else {
        "${grgit.head().id}-SNAPSHOT"
    }
}
